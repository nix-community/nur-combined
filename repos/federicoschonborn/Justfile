default_args := "--verbose --print-build-logs --log-format multiline-with-logs --show-trace"

@_default:
    just --list --unsorted

@_to-outputs +NAMES:
    echo {{ NAMES }} | sed -e 's:[^ ]*:".#&":g'

@_all-packages:
    nix flake show --json 2>/dev/null | jq -r '.packages."x86_64-linux" | keys | join(" ")'

_nix +ARGS:
    nix {{ ARGS }} {{ default_args }}

_nix-unstable +ARGS:
    just _nix {{ ARGS }} --inputs-from . --override-input nixpkgs nixpkgs

_nix-stable +ARGS:
    just _nix {{ ARGS }} --inputs-from . --override-input nixpkgs nixpkgs-stable

[group("build")]
build-unstable +PACKAGES:
    just _nix-unstable build $(just _to-outputs {{ PACKAGES }})

[group("build")]
build-stable +PACKAGES:
    just _nix-stable build $(just _to-outputs {{ PACKAGES }})

[group("build")]
build +PACKAGES: (build-unstable PACKAGES) (build-stable PACKAGES)

[group("build-all")]
build-all-unstable:
    just build-unstable $(just _all-packages)

[group("build-all")]
build-all-stable:
    just build-stable $(just _all-packages)

[group("build")]
build-all: build-all-unstable build-all-stable

[group("rebuild")]
rebuild-unstable +PACKAGES: (build-unstable PACKAGES)
    just _nix-unstable build --rebuild $(just _to-outputs {{ PACKAGES }})

[group("rebuild")]
rebuild-stable +PACKAGES: (build-stable PACKAGES)
    just _nix-stable build --rebuild $(just _to-outputs {{ PACKAGES }})

[group("rebuild")]
rebuild +PACKAGES: (rebuild-unstable PACKAGES) (rebuild-stable PACKAGES)

[group("run")]
run-unstable PACKAGE: (build-unstable PACKAGE)
    just _nix-unstable run ".#{{ PACKAGE }}"

[group("run")]
run-stable PACKAGE: (build-stable PACKAGE)
    just _nix-stable run ".#{{ PACKAGE }}"

[group("path-info")]
path-info-unstable PACKAGE: (build-unstable PACKAGE)
    just _nix-unstable path-info ".#{{ PACKAGE }}"

[group("path-info")]
path-info-stable PACKAGE: (build-stable PACKAGE)
    just _nix-stable path-info ".#{{ PACKAGE }}"

tree PACKAGE: (build-unstable PACKAGE)
    #!/usr/bin/env nix-shell
    #!nix-shell -i bash -p nix-tree
    set -euxo pipefail
    nix-tree ".#{{ PACKAGE }}"

inspect:
    #!/usr/bin/env nix-shell
    #!nix-shell -i bash -p nix-inspect
    set -euxo pipefail
    nix-inspect --path .

lint:
    #!/usr/bin/env nix-shell
    #!nix-shell -i bash -p statix deadnix
    set -euxo pipefail
    statix check
    deadnix

generate-readme:
    just _nix-unstable run ".#generate-readme"
